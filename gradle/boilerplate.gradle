//--- Gradle Dependencies

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: 'idea'

//--- Script Utilties
project.getOr = {String envProp, projectProp -> System.getenv(envProp) ?: projectProp}
project.logFile = {LogLevel logLevel, String file ->  logger.log(logLevel, new File('gradle/docs/'+file+'.txt').getText('UTF-8'))}
project.printDocs = {String file -> println(new File('gradle/docs/'+file+'.txt').getText('UTF-8'))}

//--- Project SourceSets
sourceSets {
    main
    test
}

idea {
    project {
        jdkName = '1.8'
        languageLevel = '1.8'
    }
    module {
        name file("./").name
        outputDir = compileJava.destinationDir
        testOutputDir = compileTestJava.destinationDir
    }
}

tasks.register('additionalIntellijSettings', Copy) {
    from 'gradle/docs/idea/'
    include "*"
    into '.idea'
}
tasks.idea.dependsOn additionalIntellijSettings

//--- Project Defaults
setVersion(modVersion)
setGroup(modGroup)
setArchivesBaseName(modName)

//Ensures no compilation errors due to unrecognised unicode characters
compileJava.options.encoding = 'UTF-8'
tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

//--- Settings
minecraft {
    version = modForge
    runDir = "run"

    replace "@VERSION@":modVersion
    mappings = modMappings
    useDepAts = true
}

//--- Handling custom.gradle

//To use personalized options when working with the mod create a file named custom.gradle
//The file is not sychronised with GitHub and can be used i.e. to store username and password
//Thanks to BluSunrize for the idea (taken from IE)

// We have to put it **BEFORE** any other gradle files are parsed.
def customGradle = rootProject.file('custom.gradle')
if (customGradle.exists()) {
    apply from: customGradle
}

//--- Include the ModWorks Processor
if(ii_useModworksPreprocessor.asBoolean())
    apply from: rootProject.file("gradle/modworks.gradle")

//--- Include Minecraft JUnit
if(ii_useJUnit.asBoolean())
    apply from: rootProject.file("gradle/junit.gradle")

compileJava {
    logFile(LogLevel.WARN, "gradlephant")
    logger.log(LogLevel.WARN, ii_gradlephantTitle)
    logger.log(LogLevel.WARN, ii_gradlephantLink+'\n')

    if(ii_printSourceSets.toBoolean())
    {
        logger.log(LogLevel.WARN, 'SourceSets in Project: ')
        sourceSets.each { srcSet ->
            println "> ["+srcSet.name+"]"
            print "   - Sources: "+srcSet.allJava.srcDirs+"\n"
        }
        println ""
    }

    println 'This mod uses Team II\'s Mod Setup Script, run [gradlew modSetupHelp].'

    if(ii_generateCoreJar=='true')
    {
        println 'This mod will generate a [core jar] and place it inside its [main jar] during build.'
        println 'For this, a valid fingerprint and configured keystore is needed.'
    }
    else
        println 'This mod will not generate a [core jar] inside its [main jar]. Enable it in [gradle.properties] if needed.'

    if(ii_useModworksPreprocessor)
        println 'This mod has ModWorks annotation processor enabled, check out https://github.com/Team-Immersive-Intelligence/ModworksProcessor for more information.'
    if(ii_useJUnit)
        println 'This mod has JUnit tests enabled, check out https://github.com/Team-Immersive-Intelligence/Libelle for more information.'

}

//--- Generating Deobfed Jar
tasks.register('deobfJar', Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
    manifest {
        attributes 'FMLAT': modATPath
        attributes 'Maven-Artifact': modGroup + ':' + modName + ':' + modVersion
    }
}

artifacts {
    archives deobfJar
}

//--- Resource Processing

processResources {
    //this will ensure that this task is redone when the versions change.
    inputs.property "version", modVersion
    inputs.property "mcversion", project.minecraft.version

    //copy everything else except the mcmod.info and excluded files
    //setDuplicatesStrategy('exclude')

    //replace stuff in mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        // replace properties
        expand 'version': modVersion, 'mcversion': project.minecraft.version
    }

    //Exclude GIMP images
    exclude '**/*.xcf'
    //Exclude batch
    exclude '**/*.bat'
    exclude '**/*.sh'
    //Exclude template textures
    exclude '**/*Template.png'

    //already excluded from gitignore, but still appear to be included in a locally built jar
    exclude '**/desktop.ini'
    exclude '**/Thumbs.db'
    exclude '**/.DS_Store'
}

tasks.build.doLast(){
    if(ii_printSourceSets.toBoolean())
        logger.log(LogLevel.WARN, 'Build Location: '+file("./build/libs/").getPath())
}

//--- Core Jar Generation
// Create a core jar, sign it and place it inside the main jar
if(ii_generateCoreJar=='true')
    apply from: rootProject.file('gradle/corejar.gradle')

//--- IE Addon
apply from: rootProject.file('gradle/ieaddon.gradle')

//--- Help Function
tasks.register('modSetupDocs') {
    group = "Help"
    description = "Displays the Mod Build Script documentation"
    doLast {
        printDocs("setup")
    }
}